plugins {
  id 'org.springframework.boot' version '2.1.3.RELEASE'
  id 'java'
  id "net.ltgt.apt-idea" version "0.19"
  id "com.moowork.node" version "1.2.0"
}

apply plugin: 'io.spring.dependency-management'

group = 'com.jiang'
version = '0.0.1'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

idea {
  module {
    excludeDirs += files('node_modules')
  }
}

defaultTasks 'bootRun'

repositories {
  mavenCentral()
}

if (project.hasProperty('prod')) {
  apply from: 'gradle/profile_prod.gradle'
} else {
  apply from: 'gradle/profile_dev.gradle'
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-aop:2.1.3.RELEASE'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.1.3.RELEASE'
  implementation 'org.springframework.boot:spring-boot-starter-validation:2.1.3.RELEASE'
  implementation 'org.springframework.boot:spring-boot-starter-web:2.1.3.RELEASE'
  implementation group: 'org.zalando', name: 'problem', version: '0.22.0'
  implementation group: 'org.zalando', name: 'jackson-datatype-problem', version: '0.22.0'

  compileOnly 'org.projectlombok:lombok:1.18.6'
  runtimeOnly group: 'com.h2database', name: 'h2', version: '1.4.196'

  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.6'

  testImplementation ('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'junit', module: 'junit'
  }
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.2'
  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '3.3.0'
  testImplementation group: 'io.rest-assured', name: 'spring-mock-mvc', version: '3.3.0'

  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'

}

test {
  useJUnitPlatform()
}


task cleanResources(type: Delete) {
  delete 'build/resources'
}

wrapper {
  gradleVersion = '5.2.1'
}